<@include gpu/Config.slh@>
<$VERSION_HEADER$>
//  Generated on <$_SCRIBE_DATE$>
//
//  model_translucent.frag
//  fragment shader
//
//  Created by Andrzej Kapolka on 9/19/14.
//  Copyright 2014 High Fidelity, Inc.
//
//  Distributed under the Apache License, Version 2.0.
//  See the accompanying file LICENSE or http://www.apache.org/licenses/LICENSE-2.0.html
//

<@include model/Material.slh@>

<@include DeferredLighting.slh@>
<@include model/Light.slh@>

<@include gpu/Transform.slh@>
<$declareStandardCameraTransform()$>

vec4 evalGlobalColor(float shadowAttenuation, vec3 position, vec3 normal, vec3 diffuse, vec3 specular, float gloss, float opacity) {

    // Need the light now
    Light light = getLight();
    TransformCamera cam = getTransformCamera();
    vec3 fragNormal;
    <$transformEyeToWorldDir(cam, normal, fragNormal)$>
    vec3 fragEyeVectorView = normalize(-position);
    vec3 fragEyeDir;
    <$transformEyeToWorldDir(cam, fragEyeVectorView, fragEyeDir)$>

    vec3 color = opacity * diffuse.rgb * getLightColor(light) * getLightAmbientIntensity(light);

    vec4 shading = evalFragShading(fragNormal, -getLightDirection(light), fragEyeDir, specular, gloss);

    color += vec3(diffuse * shading.w * opacity + shading.rgb) * shadowAttenuation * getLightColor(light) * getLightIntensity(light);

    return vec4(color, opacity);
}

uniform sampler2D diffuseMap;

in vec2 _texCoord0;
in vec4 _position;
in vec3 _normal;
in vec3 _color;
in float _alpha;

out vec4 _fragColor;

void main(void) {
    vec4 diffuse = texture(diffuseMap, _texCoord0);

    Material mat = getMaterial();
    vec3 fragPosition = _position.xyz;
    vec3 fragNormal = normalize(_normal);
    vec3 fragDiffuse = getMaterialDiffuse(mat) * diffuse.rgb * _color;
    vec3 fragSpecular = getMaterialSpecular(mat);
    float fragGloss = getMaterialShininess(mat) / 128;
    float fragOpacity = getMaterialOpacity(mat) * diffuse.a * _alpha;

    _fragColor =  evalGlobalColor(1.0,
        fragPosition,
        fragNormal,
        fragDiffuse,
        fragSpecular,
        fragGloss,
        fragOpacity);
}
