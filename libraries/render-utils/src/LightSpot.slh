//  Generated on <$_SCRIBE_DATE$>
//
//  Created by Sam Gateau on 7/5/16.
//  Copyright 2016 High Fidelity, Inc.
//
//  Distributed under the Apache License, Version 2.0.
//  See the accompanying file LICENSE or http://www.apache.org/licenses/LICENSE-2.0.html
//



<@func declareLightingSpot(supportScattering)@>

void evalLightingSpot(out vec3 diffuse, out vec3 specular, Light light,
    vec4 fragLightDirLen, float cosSpotAngle, vec3 fragEyeDir, vec3 normal, float roughness,
    float metallic, vec3 fresnel, vec3 albedo, float shadow
<@if supportScattering@>
    , float scattering, vec4 midNormalCurvature, vec4 lowNormalCurvature
<@endif@>
    ) {

    // Allright we re valid in the volume
    float fragLightDistance = fragLightDirLen.w;
    vec3 fragLightDir = fragLightDirLen.xyz;


    // Eval attenuation  
    float radialAttenuation = lightIrradiance_evalLightAttenuation(light.irradiance, fragLightDistance);
    float angularAttenuation = lightIrradiance_evalLightSpotAttenuation(light.irradiance, cosSpotAngle);
    vec3 lightEnergy = angularAttenuation * radialAttenuation * shadow *getLightIrradiance(light);

    // Eval shading
    evalFragShading(diffuse, specular, normal, fragLightDir, fragEyeDir, metallic, fresnel, roughness, albedo
<@if supportScattering@>
    ,scattering, midNormalCurvature, lowNormalCurvature
<@endif@>
    );
    
    diffuse *= lightEnergy * isDiffuseEnabled() * isSpotEnabled();
    specular *= lightEnergy * isSpecularEnabled() * isSpotEnabled();

    if (isShowLightContour() > 0.0) {
        // Show edges
        float edgeDistR = (lightVolume_getRadius(light.volume) - fragLightDistance);
        float edgeDistS = dot(fragLightDistance * vec2(cosSpotAngle, sqrt(1.0 - cosSpotAngle * cosSpotAngle)), -lightVolume_getSpotOutsideNormal2(light.volume));
        float edgeDist = min(edgeDistR, edgeDistS);
        float edge = abs(2.0 * (edgeDist / (0.1)) - 1.0);
        if (edge < 1.0) {
            float edgeCoord = exp2(-8.0*edge*edge);
            diffuse = vec3(edgeCoord * edgeCoord * getLightColor(light));
        }
    }
}

<@endfunc@>

<@func declareDrawSpotOutline()@>

bool evalLightSpotEdge(out vec3 color, Light light, vec4 fragLightDirLen, float cosSpotAngle, vec3 fragEyeDir) {
    // Allright we re valid in the volume
    float fragLightDistance = fragLightDirLen.w;
    vec3 fragLightDir = fragLightDirLen.xyz;
    
    // Show edges
    float edgeDistR = (lightVolume_getRadius(light.volume) - fragLightDistance);
    float edgeDistS = dot(fragLightDistance * vec2(cosSpotAngle, sqrt(1.0 - cosSpotAngle * cosSpotAngle)), -lightVolume_getSpotOutsideNormal2(light.volume));
    float edgeDist = min(edgeDistR, edgeDistS);
    float edge = abs(2.0 * (edgeDist / (0.1)) - 1.0);
    if (edge < 1) {
        float edgeCoord = exp2(-8.0*edge*edge);
        color = vec3(edgeCoord * edgeCoord * getLightColor(light));
    }

    return (edge < 1);
}

<@endfunc@>


